ind eq (A : U₀) (x : A) : A → Prop
| refl : eq A x x

def sym : Π(A : U₀) Π(x : A) Π(y : A) eq A x y → eq A y x
    := λ (A : U₀),
       λ (x : A),
       elim Prop eq A x (λ (y : A), λ (_ : eq A x y), eq A y x)
            (refl A x)

def trans : Π(A : U₀) Π(x : A) Π(y : A) Π(z : A) eq A x y → eq A y z → eq A x z
    := λ (A : U₀),
       λ (x : A),
       λ (y : A),
       λ (z : A),
       elim Prop eq A x (λ (y₀ : A), λ (_ : eq A x y₀), eq A y₀ z → eq A x z)
            (λ (p : eq A x z), p) y

def ap : Π (A : U₀) Π (B : U₀) Π (f : A → B) Π (x : A) Π (y : A) eq A x y → eq B (f x) (f y)
    := λ (A : U₀),
       λ (B : U₀),
       λ (f : A → B),
       λ (x : A),
         elim Prop eq A x (λ (y : A), λ (_ : eq A x y), eq B (f x) (f y))
              (refl B (f x))

ind top : Prop
| truth : top

ind bot : Prop

def not : Prop → Prop
    := λ (A : Prop), A → bot

ind nat : U₀
| zero : nat
| succ : nat → nat

def add : nat → nat → nat
    := elim U₀ nat (λ (_ : nat), nat → nat) (λ (n : nat), n) (λ (_ : nat), λ (addRec : nat → nat), λ (n : nat),  succ (addRec n))

def mul : nat → nat → nat
    := elim U₀ nat (λ (_ : nat), nat → nat) (λ (_ : nat), zero) (λ (_ : nat), λ (mulRec : nat → nat), λ (n : nat), add n (mulRec n))

def add_zero_r : Π(n : nat) eq nat (add n zero) n
    := elim Prop nat (λ (n : nat), eq nat (add n zero) n)
            (refl nat zero)
            (λ (n : nat),
             λ (ih : eq nat (add n zero) n),
               ap nat nat succ (add n zero) n ih
            )

def add_succ_r : Π(n : nat) Π(m : nat) eq nat (add n (succ m)) (succ (add n m))
    := elim Prop nat (λ (n : nat), Π(m : nat) eq nat (add n (succ m)) (succ (add n m)))
            (λ (m : nat), refl nat (succ m))
            (λ (n : nat),
             λ (ih : Π(m : nat) eq nat (add n (succ m)) (succ (add n m))),
             λ (m : nat),
               ap nat nat succ (add n (succ m)) (succ (add n m)) (ih m)
            )

def add_comm : Π(n : nat) Π(m : nat) eq nat (add n m) (add m n)
    := elim Prop nat (λ (n : nat), Π(m : nat) eq nat (add n m) (add m n))
                 (λ (m : nat), sym nat (add m zero) m (add_zero_r m))
                 (λ (n : nat),
                  λ (ih : Π(m : nat) eq nat (add n m) (add m n)),
                  λ (m : nat),
                    trans nat (succ (add n m)) (succ (add m n)) (add m (succ n))
                          (ap nat nat succ (add n m) (add m n) (ih m))
                          (sym nat (add m (succ n)) (succ (add m n)) (add_succ_r m n))
                 )
def add_assoc : Π(n : nat) Π(m : nat) Π(k : nat) eq nat (add n (add m k)) (add (add n m) k)
    := elim Prop nat (λ (n : nat), Π(m : nat) Π(k : nat) eq nat (add n (add m k)) (add (add n m) k))
       (λ (m : nat), λ (k : nat), refl nat (add m k))
       (λ (n : nat),
        λ (ih : Π(m : nat) Π(k : nat) eq nat (add n (add m k)) (add (add n m) k)),
        λ (m : nat),
        λ (k : nat),
        ap nat nat succ (add n (add m k)) (add (add n m) k) (ih m k)
       )

def bot_above_succ : nat → Prop
    := elim U₀ nat (λ (_ : nat), Prop) top (λ (_ : nat), λ (_ : Prop), bot)
    
def zero_neq_succ : Π(n : nat) not (eq nat zero (succ n))
    := λ (n : nat),
       λ (p : eq nat zero (succ n)),
         elim Prop eq nat zero
              (λ (n₀ : nat), λ (_ : eq nat zero n₀), bot_above_succ n₀)
              truth (succ n) p