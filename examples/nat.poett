-- Natural numbers
#import examples/equality

ind nat : U₀
| zero : nat
| succ : nat → nat

def add : nat → nat → nat
    := elim U₀ nat (λ (_ : nat), nat → nat) (λ (n : nat), n) (λ (_ : nat), λ (addRec : nat → nat), λ (n : nat),  succ (addRec n))

def mul : nat → nat → nat
    := elim U₀ nat (λ (_ : nat), nat → nat) (λ (_ : nat), zero) (λ (_ : nat), λ (mulRec : nat → nat), λ (n : nat), add n (mulRec n))

def add-zero-r : Π(n : nat) eq nat (add n zero) n
    := elim Prop nat (λ (n : nat), eq nat (add n zero) n)
            (refl nat zero)
            (λ (n : nat),
             λ (ih : eq nat (add n zero) n),
               ap nat nat succ (add n zero) n ih
            )

def add-succ-r : Π(n : nat) Π(m : nat) eq nat (add n (succ m)) (succ (add n m))
    := elim Prop nat (λ (n : nat), Π(m : nat) eq nat (add n (succ m)) (succ (add n m)))
            (λ (m : nat), refl nat (succ m))
            (λ (n : nat),
             λ (ih : Π(m : nat) eq nat (add n (succ m)) (succ (add n m))),
             λ (m : nat),
               ap nat nat succ (add n (succ m)) (succ (add n m)) (ih m)
            )

def add-comm : Π(n : nat) Π(m : nat) eq nat (add n m) (add m n)
    := elim Prop nat (λ (n : nat), Π(m : nat) eq nat (add n m) (add m n))
                 (λ (m : nat), sym nat (add m zero) m (add-zero-r m))
                 (λ (n : nat),
                  λ (ih : Π(m : nat) eq nat (add n m) (add m n)),
                  λ (m : nat),
                    trans nat (succ (add n m)) (succ (add m n)) (add m (succ n))
                          (ap nat nat succ (add n m) (add m n) (ih m))
                          (sym nat (add m (succ n)) (succ (add m n)) (add-succ-r m n))
                 )
def add-assoc : Π(n : nat) Π(m : nat) Π(k : nat) eq nat (add n (add m k)) (add (add n m) k)
    := elim Prop nat (λ (n : nat), Π(m : nat) Π(k : nat) eq nat (add n (add m k)) (add (add n m) k))
       (λ (m : nat), λ (k : nat), refl nat (add m k))
       (λ (n : nat),
        λ (ih : Π(m : nat) Π(k : nat) eq nat (add n (add m k)) (add (add n m) k)),
        λ (m : nat),
        λ (k : nat),
        ap nat nat succ (add n (add m k)) (add (add n m) k) (ih m k)
       )

def bot-above-succ : nat → Prop
    := elim U₀ nat (λ (_ : nat), Prop) top (λ (_ : nat), λ (_ : Prop), bot)
    
def zero-neq-succ : Π(n : nat) not (eq nat zero (succ n))
    := λ (n : nat),
       λ (p : eq nat zero (succ n)),
         elim Prop eq nat zero
              (λ (n₀ : nat), λ (_ : eq nat zero n₀), bot-above-succ n₀)
              I (succ n) p

def mul-zero-r : Π(n : nat) eq nat (mul n zero) zero
    := elim Prop nat (λ (n : nat), eq nat (mul n zero) zero)
            (refl nat zero)
            (λ (n : nat),
             λ (ih : eq nat (mul n zero) zero), ih)

def mul-succ-r : Π(n : nat) Π(m : nat) eq nat (mul n (succ m)) (add n (mul n m))
    := elim Prop nat (λ (n : nat), Π(m : nat) eq nat (mul n (succ m)) (add n (mul n m)))
            (λ (_ : nat), refl nat zero)
            (λ (n : nat),
             λ (ih : Π(m : nat) eq nat (mul n (succ m)) (add n (mul n m))),
             λ (m : nat),
               ap nat nat succ (add m (mul n (succ m))) (add n (add m (mul n m)))
                    (trans nat (add m (mul n (succ m)))
                               (add m (add n (mul n m)))
                               (add n (add m (mul n m)))
                               (ap nat nat (add m) (mul n (succ m)) (add n (mul n m)) (ih m))
                               (trans nat (add m (add n (mul n m)))
                                          (add (add m n) (mul n m))
                                          (add n (add m (mul n m)))
                                          (add-assoc m n (mul n m))
                                          (trans nat (add (add m n) (mul n m))
                                                     (add (add n m) (mul n m))
                                                     (add n (add m (mul n m)))
                                                     (ap nat nat (λ (k : nat), add k (mul n m)) (add m n) (add n m) (add-comm m n))
                                                     (sym nat
                                                         (add n (add m (mul n m)))
                                                         (add (add n m) (mul n m))
                                                         (add-assoc n m (mul n m)))
                                          )
                               )
                     )
            )
             
def mul-comm : Π(n : nat) Π(m : nat) eq nat (mul n m) (mul m n)
    := elim Prop nat (λ (n : nat), Π(m : nat) eq nat (mul n m) (mul m n))
            (λ (m : nat), sym nat (mul m zero) zero (mul-zero-r m))
            (λ (n : nat),
             λ (ih : Π(m : nat) eq nat (mul n m) (mul m n)),
             λ (m : nat),
               trans nat (mul (succ n) m)
                         (add m (mul m n))
                         (mul m (succ n))
                         (ap nat nat (add m) (mul n m) (mul m n) (ih m))
                         (sym nat (mul m (succ n)) (add m (mul m n)) (mul-succ-r m n))
            )

def mul-add-distr-l : Π(n : nat) Π(m : nat) Π(k : nat) eq nat (mul n (add m k)) (add (mul n m) (mul n k))
    := elim Prop nat (λ (n : nat), Π(m : nat) Π(k : nat) eq nat (mul n (add m k)) (add (mul n m) (mul n k)))
            (λ (_ : nat), λ (_ : nat), refl nat zero)
            (λ (n : nat),
             λ (ih : Π(m : nat) Π(k : nat) eq nat (mul n (add m k)) (add (mul n m) (mul n k))),
             λ (m : nat),
             λ (k : nat),
              trans nat (add (add m k) (mul n (add m k)))
                        (add (add m k) (add (mul n m) (mul n k)))
                        (add (add m (mul n m)) (add k (mul n k)))
                        (ap nat nat (add (add m k)) (mul n (add m k)) (add (mul n m) (mul n k)) (ih m k))
                        (trans nat (add (add m k) (add (mul n m) (mul n k)))
                                   (add m (add k (add (mul n m) (mul n k))))
                                   (add (add m (mul n m)) (add k (mul n k)))
                                   (sym nat (add m (add k (add (mul n m) (mul n k))))
                                            (add (add m k) (add (mul n m) (mul n k)))    
                                            (add-assoc m k (add (mul n m) (mul n k)))
                                   )
                                   (trans nat (add m (add k (add (mul n m) (mul n k))))
                                              (add m (add (add k (mul n m)) (mul n k)))
                                              (add (add m (mul n m)) (add k (mul n k)))
                                              (ap nat nat (add m)
                                                          (add k (add (mul n m) (mul n k)))
                                                          (add (add k (mul n m)) (mul n k))
                                                          (add-assoc k (mul n m) (mul n k))
                                              ) 
                                              (trans nat (add m (add (add k (mul n m)) (mul n k)))
                                                         (add m (add (add (mul n m) k) (mul n k)))
                                                         (add (add m (mul n m)) (add k (mul n k)))
                                                         (ap nat nat (λ (n₀ : nat), add m (add n₀ (mul n k)))
                                                                     (add k (mul n m))
                                                                     (add (mul n m) k)
                                                                     (add-comm k (mul n m))
                                                             
                                                         )
                                                         (trans nat (add m (add (add (mul n m) k) (mul n k)))
                                                                    (add m (add (mul n m) (add k (mul n k))))
                                                                    (add (add m (mul n m)) (add k (mul n k)))
                                                                    (ap nat nat (add m)
                                                                        (add (add (mul n m) k) (mul n k))
                                                                        (add (mul n m) (add k (mul n k)))
                                                                        (sym nat (add (mul n m) (add k (mul n k)))
                                                                                 (add (add (mul n m) k) (mul n k))
                                                                                 (add-assoc (mul n m) k (mul n k))
                                                                        )
                                                                    )
                                                                    (add-assoc m (mul n m) (add k (mul n k)))
                                                         )
                                              )
                                    )
                        )
            )

def mul-add-distr-r : Π(n : nat) Π(m : nat) Π(k : nat) eq nat (mul (add n m) k) (add (mul n k) (mul m k))
    := λ (n : nat),
       λ (m : nat),
       λ (k : nat),
          trans nat (mul (add n m) k)
                    (mul k (add n m))
                    (add (mul n k) (mul m k))
                    (mul-comm (add n m) k)
                    (trans nat (mul k (add n m))
                               (add (mul k n) (mul k m))
                               (add (mul n k) (mul m k))
                               (mul-add-distr-l k n m)
                               (trans nat (add (mul k n) (mul k m))
                                          (add (mul n k) (mul k m))
                                          (add (mul n k) (mul m k))
                                          (ap nat nat (λ (n₀ : nat), add n₀ (mul k m))
                                                      (mul k n) (mul n k) (mul-comm k n)
                                          )
                                          (ap nat nat (add (mul n k))
                                                      (mul k m) (mul m k) (mul-comm k m)
                                          )

                               )
                    )
                    
def mul-assoc : Π(n : nat) Π(m : nat) Π(k : nat) eq nat (mul n (mul m k)) (mul (mul n m) k)
   := elim Prop nat (λ (n : nat), Π(m : nat) Π(k : nat) eq nat (mul n (mul m k)) (mul (mul n m) k))
           (λ (_ : nat), λ (_ : nat), refl nat zero)
           (λ (n : nat),
            λ (ih : Π(m : nat) Π(k : nat) eq nat (mul n (mul m k)) (mul (mul n m) k)),
            λ (m : nat),
            λ (k : nat),
             trans nat (add (mul m k) (mul n (mul m k)))
                       (add (mul m k) (mul (mul n m) k))
                       (mul (add m (mul n m)) k)
                       (ap nat nat (add (mul m k)) (mul n (mul m k)) (mul (mul n m) k) (ih m k))
                       (sym nat (mul (add m (mul n m)) k)
                                (add (mul m k) (mul (mul n m) k))
                                (mul-add-distr-r m (mul n m) k)
                       )
                       
           )