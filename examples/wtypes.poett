#import examples/inductives

ind W (A : U₀) (B : A → U₀) : U₀
| node : Π(a : A) Π (f : B a → W A B) W A B

-- Extensional equality on W-types
def W-eq : Π(A : U₀) Π(B : A → U₀) W A B → W A B → Prop
    := λ (A : U₀),
       λ (B : A → U₀),
         elim U₀ W A B (λ (_ : W A B), W A B → Prop)
              (λ (a : A),
               λ (f : B a → W A B),
               λ (ih : B a → W A B → Prop),
                     elim U₀ W A B (λ (_ : W A B), Prop)
                          (λ (a₀ : A),
                           λ (g : B a₀ → W A B),
                           λ (_ : B a₀ → Prop),
                             ex (eq A a a₀) (λ (p : eq A a a₀), Π(b : B a) ih b (g (elim U₀ eq A a (λ (a₁ : A), λ (_ : eq A a a₁), B a₁) b a₀ p)))
                           )
              )

def W-eq-refl : Π(A : U₀) Π(B : A → U₀) Π(w : W A B) W-eq A B w w
    := λ (A : U₀),
       λ (B : A → U₀),
         elim Prop W A B (λ (w : W A B), W-eq A B w w)
              (λ (a : A),
               λ (f : B a → W A B),
               λ (ih : Π(b : B a) W-eq A B (f b) (f b)),
                 prEx (eq A a a)
                      (λ (p : eq A a a), Π(b : B a) W-eq A B (f b) (f (elim U₀ eq A a (λ (a₀ : A), λ (_ : eq A a a₀), B a₀) b a p)))
                      (refl A a) ih
              )

def wlistNodes : U₀ → U₀
    := λ (A : U₀), sum unit A

def wlistAr : Π(A : U₀) wlistNodes A → U₀
    := λ (A : U₀), elim U₁ sum unit A (λ (_ : sum unit A), U₀) (λ (_ : unit), empty) (λ (_ : A), unit)

def wlist : U₀ → U₀
    := λ (A : U₀), W (wlistNodes A) (wlistAr A)

def wnil : Π(A : U₀) wlist A
    := λ (A : U₀), node (wlistNodes A) (wlistAr A) (inl unit A tt) (elim U₀ empty (λ (_ : empty), wlist A))

def wcons : Π(A : U₀) A → wlist A → wlist A
    := λ (A : U₀), λ (h : A), λ (t : wlist A), node (wlistNodes A) (wlistAr A) (inr unit A h) (λ (_ : unit), t)

def listToWlist : Π(A : U₀) list A → wlist A
    := λ (A : U₀), elim U₀ list A (λ (_ : list A), wlist A) (wnil A) (λ (h : A), λ (_ : list A), λ (t : wlist A), wcons A h t)

def wlistToList : Π(A : U₀) wlist A → list A
    := λ (A : U₀),
       elim U₀ W (wlistNodes A) (wlistAr A)
            (λ (_ : W (wlistNodes A) (wlistAr A)), list A)
            (elim U₀ sum unit A
                  (λ (x : sum unit A), (wlistAr A x → wlist A) → (wlistAr A x → list A) → list A)
                  (λ (_ : unit), λ (_ : empty → wlist A), λ (_ : empty → list A), nil A)
                  (λ (h : A), λ (_ : unit → wlist A), λ (t : unit → list A), cons A h (t tt))
             )

def foo : list unit
    := cons unit tt (cons unit tt (nil unit))

def bar : wlist unit
    := listToWlist unit foo

def oof : list unit
    := wlistToList unit bar


#check (refl (list unit) foo : eq (list unit) foo oof)

#nf foo
#nf bar
#nf oof
